plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser'
}

def libraryGroup    = 'io.github.neboskreb'
def libraryArtifact = 'suppress-logs'
def libraryVersion  = '1.2.1-SNAPSHOT'


final stagingDirectory = layout.buildDirectory.dir('staging-deploy')

publishing {
    repositories {
        maven {
            name = 'staging'
            url = stagingDirectory
        }
    }

    publications {
        mavenJava(MavenPublication) {
            group = libraryGroup
            artifactId = libraryArtifact
            version = libraryVersion

            from components.java

            pom {
                name = 'Suppress Logs extension'
                description = 'JUnit 5 extension for easy suppression of unwanted error logs'
                url = 'https://github.com/neboskreb/suppress-logs'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'JBM'
                        name = 'John Y. Pazekha'
                        email = 'neboskreb@narod.ru'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:neboskreb/suppress-logs.git'
                    developerConnection = 'scm:git:ssh://github.com:neboskreb/suppress-logs.git'
                    url = 'https://github.com/neboskreb/suppress-logs'
                }
            }
        }

    }
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'RELEASE'
        armored = true
        verify = false

        version = libraryVersion

        secretKey  = getConfiguredValue('signing.key.armored', 'PGP_SIGNING_KEY_ARMORED')
        passphrase = getConfiguredValue('signing.password',    'PGP_SIGNING_PASSWORD')
    }
    deploy {
        maven {
            final String staging = stagingDirectory.get().toString()

            github {
                packages {
                    active = 'RELEASE'
                    url = 'https://maven.pkg.github.com/neboskreb/suppress-logs'
                    stagingRepository(staging)

                    sign = true
                    applyMavenCentralRules = true

                    username = getConfiguredValue('github_packages_user', 'PACKAGES_USERNAME')
                    password = getConfiguredValue('github_packages_token', 'PACKAGES_TOKEN')
                }
            }

            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository(staging)

                    sign = true
                    applyMavenCentralRules = true

                    username = getConfiguredValue('mavenCentralUser', 'MAVEN_USERNAME')
                    password = getConfiguredValue('mavenCentralPass', 'MAVEN_PASSWORD')
                }
            }
        }
    }
}

tasks.named('jreleaserDeploy') {
    dependsOn publishMavenJavaPublicationToStagingRepository
}

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = 11
    targetCompatibility = 11
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    compileOnly    platform('org.junit:junit-bom:[5.0.0, 6)')
    compileOnly    'org.junit.jupiter:junit-jupiter'
    compileOnly    'org.junit.jupiter:junit-jupiter-engine'
    compileOnly    'org.slf4j:slf4j-api:2.0.17'
    compileOnly    'org.slf4j:slf4j-log4j12:2.0.17'
    compileOnly    'org.slf4j:slf4j-jdk14:2.0.17'
    compileOnly    'ch.qos.logback:logback-classic:1.5.26'
}


private String getConfiguredValue(String projectProperty, String environmentVariable) {
    return findProperty(projectProperty) ?: System.getenv(environmentVariable)
}
